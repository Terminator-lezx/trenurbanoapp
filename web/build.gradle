buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE")
    }
}
plugins {
    id "com.moowork.node" version '0.11'
    id "com.moowork.gulp" version '0.11'
}

description = 'Tren Urbano App Web'

apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: "com.moowork.node"
apply plugin: "com.moowork.gulp"

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ['install']
    inputs.files file('bower.json')
    outputs.files file('src/main/webapp/jspkg')
}

// processes your package.json before running gulp build
bowerInstall.dependsOn 'npmInstall'

// runs "bower install"
gulp_default.dependsOn 'bowerInstall'

// runs "gulp build" as part of your gradle build
build.dependsOn gulp_default

configurations {
    all {
        exclude module: 'spring-boot-starter-logging'
    }
    providedRuntime
}

dependencies {
    compile project(':webapi-client')
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: 'spring-boot-starter-logging'
    }
    compile "org.springframework.boot:spring-boot-starter-jdbc"
    compile "org.springframework.boot:spring-boot-starter-log4j2"
    providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"
    providedRuntime "org.apache.tomcat.embed:tomcat-embed-jasper"

    compile 'org.springframework:spring-context-support'
    compile "org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4:1.16"

    compile 'javax.servlet:jstl'
    compile "de.jollyday:jollyday:0.5.1"
    compile "com.zaxxer:HikariCP:2.4.1"
    compile("org.postgis:postgis-jdbc:1.3.3") {
        exclude module: 'postgresql'
    }
    compile "org.postgresql:postgresql:9.4-1202-jdbc42"
    compile "com.google.guava:guava:19.0"
    compile "de.grundid.opendatalab:geojson-jackson:1.5.1"
    compile "javax.inject:javax.inject:1"

    compile "org.apache.commons:commons-lang3:3.4"
    compile 'org.apache.httpcomponents:httpclient'
    compile 'org.apache.httpcomponents:httpcore:4.4.3'

    //Icon generation
    def batikVersion = '1.8'
    compile "org.apache.xmlgraphics:batik-transcoder:$batikVersion"
    compile "org.apache.xmlgraphics:batik-codec:$batikVersion"
    compile "org.apache.xmlgraphics:xmlgraphics-commons:2.0.1"

    compile 'org.apache.velocity:velocity'
    compile('org.apache.velocity:velocity-tools') {
        exclude group: 'org.apache.struts'
        exclude group: 'oro'
    }

}
